---
layout: post
title: Perl Weekly Challenge 127.
comments: true
excerpt: Disjoint Sets and Conflict Intervals.

tags:
   - pwc
   - perl
---

My solutions
([task 1],
and
[[https://github.com/wlmb/perlweeklychallenge-club/blob/master/challenge-127/wlmb/perl/ch-2.pl][task 2]
)
to the  [[https://perlweeklychallenge.org/blog/perl-weekly-challenge-126][The Weekly Challenge - 127](https://github.com/wlmb/perlweeklychallenge-club/blob/master/challenge-127/wlmb/perl/ch-1.pl).


# Task 1: Disjoint Sets

    Submitted by: Mohammad S Anwar
    You are given two sets with unique integers.

    Write a script to figure out if they are disjoint.

    The two sets are disjoint if they don’t have any common
    members.

    Example
    Input: @S1 = (1, 2, 5, 3, 4)
           @S2 = (4, 6, 7, 8, 9)
    Output: 0 as the given two sets have common member 4.

    Input: @S1 = (1, 3, 5, 7, 9)
           @S2 = (0, 2, 4, 6, 8)
    Output: 1 as the given two sets do not have common member.

Assuming the sets are given by numbers, as in the examples
above, there is a nice solution using the *Perl Data Language*
`PDL`. If *s1<sub>i</sub>* are the members of the first set and
*s2<sub>j</sub>* the members, then the sets are disjoint provided
all the elements of the matrix *m<sub>ij</sub>=s1<sub>i</sub>-s2<sub>j</sub>* are
different from zero. `PDL` allows to easily promote a vector
*s2<sub>i</sub>* to a matrix *m2<sub>ij</sub>=s2<sub>j</sub>* (for any *i*) by adding *dummy
dimensions*. Then, they it may be subtracted from *s1*, which
is automátically promoted to a matrix *m1<sub>ij</sub>=s1<sub>i</sub>*
and check the results for the presence of a zero entry. This
fits into a oneliner:

    perl -MPDL -E '($s1,$s2)= map {pdl $_} @ARGV;\
                  say "Input:\n\t$s1\n\t$s2\nOutput: ",($s1-$s2->dummy(0))->all' \
    	      "[1,2,5,3,4]" "[4,6,7,8,9]"

Results:

    Input:
    	[1 2 5 3 4]
    	[4 6 7 8 9]
    Output: 0

    perl -MPDL -E '($s1,$s2)= map {pdl $_} @ARGV;\
                  say "Input:\n\t$s1\n\t$s2\nOutput: ",($s1-$s2->dummy(1))->all' \
    	      "[1,3,5,7,9]" "[0,2,4,6,8]"

Results:

    Input:
    	[1 3 5 7 9]
    	[0 2 4 6 8]
    Output: 1

The full version would be:

    # Perl weekly challenge 126
    # Task 1:  disjoint sets
    #
    # See https://wlmb.github.io/2021/08/20/PWC126/#task-1-count-numbers
    use warnings;
    use strict;
    use v5.12;
    use PDL;

    die 'Usage: ./ch-1.pl "[a1,a2,...]" "[b1,b2,...]" ... to test for disjointness'
        unless @ARGV && @ARGV%2==0;
    # Assume arguments of the form "[n1,n2,n3...]"
    while(scalar @ARGV){
        my ($s1, $s2)=map {pdl $_} splice @ARGV,0,2;
        my $m=$s1-$s2->dummy(0);
        my $disjoint=$m->all;
        my $repeated=$s2->where($m->andover==0);
        say "Input;\n\t$s1\n\t$s2\nOutput: $disjoint";
        say $disjoint?"As there are no common members":"As the subset $repeated is common"
    }

Example:

    ./ch-1.pl "[1,2,5,3,4]" "[4,6,7,8,9]" "[1,3,5,7,9]" "[0,2,4,6,8]"

Results:

    Input;
    	[1 2 5 3 4]
    	[4 6 7 8 9]
    Output: 0
    As the subset [4] is common
    Input;
    	[1 3 5 7 9]
    	[0 2 4 6 8]
    Output: 1
    As there are no common members


# Task 2: Conflict Intervals

    Submitted by: Mohammad S Anwar
    You are given a list of intervals.

    Write a script to find out if the current interval conflicts
    with any of the previous intervals.

    Example
    Input: @Intervals = [ (1,4), (3,5), (6,8), (12, 13), (3,20) ]
    Output: [ (3,5), (3,20) ]

        - The 1st interval (1,4) do not have any previous
          intervals to compare with, so skip it.
        - The 2nd interval (3,5) does conflict with previous
          interval (1,4).
        - The 3rd interval (6,8) do not conflicts with any of the
          previous intervals (1,4) and (3,5), so skip it.
        - The 4th interval (12,13) again do not conflicts with any
          of the previous intervals (1,4), (3,5) and (6,8), so
          skip it.
        - The 5th interval (3,20) conflicts with the first
          interval (1,4).

    Input: @Intervals = [ (3,4), (5,7), (6,9), (10, 12), (13,15) ]
    Output: [ (6,9) ]

        - The 1st interval (3,4) do not have any previous
          intervals to compare with, so skip it.
        - The 2nd interval (5,7) do not conflicts with the
          previous interval (3,4), so skip it.
        - The 3rd interval (6,9) does conflict with one of the
          previous intervals (5,7).
        - The 4th interval (10,12) do not conflicts with any of
          the previous intervals (3,4), (5,7) and (6,9), so skip
          it.
        - The 5th interval (13,15) do not conflicts with any of
          the previous intervals (3,4), (5,7), (6,9) and (10,12),
          so skip it.

Although *conflicting* is not defined, from the examples it seems
to mean *overlapping*. Thus, an interval conflicts with a previous
one if its intersection is not empty. I assume the intervals
are open, so they don't contain their end-points, and
non-empty. Then, two intervals *(a,b)* and *(c,d)* intersect iff *c<b* and *a<d*.
I can use the *Perl Data Language* `PDL` to solve the task. I
read all intervals as pair of numbers from `STDIN` into a 2D
array with the first index denoting the left or right
boundary and the second index numbering the intervals. I use
*dummy indices* to test each interval against all other
intervals to build a matrix with 1's for each pair *ij* of
intervals that overlap and 0's elsewhere. I select the upper
right corner, corresponding to intervals that overlap previous
intervals. Finally, I get the indices of conflicting intervals
and use them to extract the conflicting intervals. This takes
only 5 lines of `PDL` code.

    # Perl weekly challenge 127
    # Task 2:  Conflict intervals
    #
    # See https://wlmb.github.io/2021/08/20/PWC126/#task-2-mineseeper-game
    use warnings;
    use strict;
    use v5.12;
    use PDL;
    use PDL::IO::Misc;
    use PDL::NiceSlice;
    # read all intervals into a 2D array
    my $ints=(rcols *STDIN,[])->transpose;
    # build matrix: overlap(i,j)=1 iff i and j overlap
    my $overlap=($ints((0),:,*1)<$ints((1),*1,:))&($ints((1),:,*1)>$ints((0),*1,:));
    # Select intervals that overlap previous ones
    my $overlap_previous=$overlap&($overlap->xvals>$overlap->yvals);
    # Indices of conflicting intervals
    my $conflicting_indices=$overlap_previous->transpose->orover->which;
    # The actual conflicting intervals.
    my $conf_ints=$ints->transpose->($conflicting_indices)->transpose;
    # Print result
    say "Input: $ints\nConflicting intervals: ",
         $conf_ints->isempty?"None":"$conf_ints\nConflicts array: $overlap_previous";

Example 1:

    ./ch-2.pl << " END"
      1  4
      3  5
      6  8
     12 13
      3 20

Results:

    Input:
    [
     [ 1  4]
     [ 3  5]
     [ 6  8]
     [12 13]
     [ 3 20]
    ]

    Conflicting intervals:
    [
     [ 3  5]
     [ 3 20]
    ]

    Conflicts array:
    [
     [0 1 0 0 1]
     [0 0 0 0 1]
     [0 0 0 0 1]
     [0 0 0 0 1]
     [0 0 0 0 0]
    ]

Example 2:

    ./ch-2.pl <<" END"
      3  4
      5  7
      6  9
     10 12
     13 15
     END

    ./ch-2.pl <<" END"
      1   4
      4.5 5
      6   8
     12  13
     15  20
     END
