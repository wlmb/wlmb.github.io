---
layout: post
title: Perl Weekly Challenge 177.
comments: true
excerpt: Damm algorithm and Palindromic prime cyclops
tags:
   - pwc
   - perl
---

My solutions
([task 1](https://github.com/wlmb/perlweeklychallenge-club/blob/master/challenge-177/wlmb/perl/ch-1.pl)
and
[task 2](https://github.com/wlmb/perlweeklychallenge-club/blob/master/challenge-177/wlmb/perl/ch-2.pl)
)
to the  [The Weekly Challenge - 177](https://theweeklychallenge.org/blog/perl-weekly-challenge-177).


# Task 1: Damm Algorithm

    Submitted by: Mohammad S Anwar
    You are given a positive number, $n.

    Write a script to validate the given number against the
    included check digit.

    Please checkout the wikipedia page for information.

    Example 1
    Input: $n = 5724
    Output: 1 as it is valid number
    Example 2
    Input: $n = 5727
    Output: 0 as it is invalid number

The Damm algorithm is useful for detecting changes in a
single symbol within a sequence, as well as a single
transposition between adjacent symbols. It is based on a
*weak totally anti-symmetric group* consisting of an operation
∗ on the possible elements of the sequence, meaning, it obeys
(c∗x)∗y=(c∗y)∗x⇒x=y for all elements c, x, y. The group is
characterized by a table. Constructing the table is a more
challenging task than using it. The [Wikipedia](https://en.wikipedia.org/wiki/Damm_algorithm) article contains
a sample table for the digits 0..9, namely

    ∗ 0 1 2 3 4 5 6 7 8 9
    0 0 3 1 7 5 9 8 6 4 2
    1 7 0 9 2 1 5 4 8 6 3
    2 4 2 0 6 8 7 1 3 5 9
    3 1 7 5 0 9 8 3 4 2 6
    4 6 1 2 3 0 4 5 9 7 8
    5 3 6 7 4 2 0 9 5 8 1
    6 5 8 6 9 7 2 0 1 3 4
    7 8 9 4 5 3 6 2 0 1 7
    8 9 4 3 8 6 1 7 2 0 5
    9 2 5 8 1 4 3 6 7 9 0

Using the table is quite simple. To test the sequence
N<sub>0</sub>N<sub>1</sub>N<sub>2</sub>…N<sub>m</sub>, where the last digit is the check digit
I evaluate
(…((0∗N<sub>0</sub>)∗N<sub>1</sub>)…)∗N<sub>m</sub>
If the result is 0, then there are no simple errors as
described above. This yields a simple 3-liner.

    perl -MList::Util=reduce -E 'my @d= split "", "03175986427092154863420687135917509834266123045978"
    ."36742095815869720134894536201794386172052581436790";	my $t=[map {[@d[10*$_..10*$_+9]]}(0..9)];
    say "$_->", (reduce {$t->[$a][$b]} (0,split "", $_))?0:1 for @ARGV;
    ' 5724 5727

Results:

    5724->1
    5727->0

The reason this works is that after a mistaken digit, say,
replacing N<sub>k</sub> by a different digit M<sub>k</sub>, the
subsequent partial sequences
(…((0∗N<sub>0</sub>)∗N<sub>1</sub>)…)∗M<sub>k</sub>)∗N<sub>k+1</sub>)…)∗N<sub>k+j</sub> do not
coincide, for any j, with the original value
(…((0∗N<sub>0</sub>)∗N<sub>1</sub>)…)∗N<sub>k</sub>∗N<sub>k+1</sub>)…)∗N<sub>k+j</sub>, i.e., a change
of one digit produces a change in a partial product that is
not corrected by subsequent partial products. Similarly, a
transposition produces a change in the partial product
(…((0∗N<sub>0</sub>)∗N<sub>1</sub>)…)∗N<sub>k+1</sub>)∗N<sub>k</sub>) that is not corrected by
subsequent partial products
(…((0∗N<sub>0</sub>)∗N<sub>1</sub>)…)∗N<sub>k+1</sub>)∗N<sub>k</sub>)\*N<sub>k+2</sub>…)∗N<sub>k+j</sub>. Choosing
the *multiplication* table with zeroes in the diagonal, i.e.,
x∗x=0, allows us to choose the check digit as the product of
all previous digits.

The full code is

     1  # Perl weekly challenge 177
     2  # Task 1: Damm algorithm
     3  #
     4  # See https://wlmb.github.io/2022/08/08/PWC177/#task-1-damm-algorithm
     5  use v5.36;
     6  use List::Util qw(reduce);
     7  die "Usage: $0 N1 [N2... ]\nTo check N_{i} with Damm's algorithm\n" unless @ARGV;
     8  my @digits= map {split "", $_} # consecutive digits of a Damm table
     9      qw(0317598642 7092154863 4206871359 1750983426 6123045978
    10         3674209581 5869720134 8945362017 9438617205 2581436790);
    11  # Organice the digits as a 2D array
    12  my $table=[map {[@digits[10*$_..10*$_+9]]}(0..9)];
    13  # The group operation $n ∗ $m is given by $table->[$n][$m]
    14  say "$_ ", (reduce {$table->[$a][$b]} (0,split "", $_))?"isn't":"is", " correct"  for @ARGV;

Example:

    ./ch-1.pl 5724 5727

Results:

    5724 is correct
    5727 isn't correct

Here is a version in Emacs-Lisp

    (defun show-damm (n)
      "Print if string of digits n is correct by applying Damm's algorithm"
      (princ (format "%s %s correct " n (if (test-damm n) "is" "isn't"))))
    (defun test-damm (n)
      "Test string of digits n for Damm-correctness"
      (equal 0(test-damm1 (cons 0 (mapcar 'string-to-number(split-string n "" t))))))
    (defun test-damm1 (s)
      "Test the sequence of digits s for Damm-correctness"
      (cl-reduce (lambda (i j)(aref2 table i j)) s))
    (defun aref2 (table i j)
      "Access 2D table with two indices"
      (aref (aref table i) j))
    (let
        (
         (table				; Damm table
          (vconcat
           (mapcar
    	(lambda(x)
    	  (vconcat
    	   (mapcar 'string-to-number (split-string x "" t))))
    	'("0317598642" "7092154863" "4206871359" "1750983426" "6123045978"
              "3674209581" "5869720134" "8945362017" "9438617205" "2581436790"))))
         (candidates '("5724" "5727")))	; examples to test
         (mapc 'show-damm candidates))

Results:

    "5724 is correct 5727 isn't correct "


# Task 2: Palindromic Prime Cyclops

    Submitted by: Mohammad S Anwar

    Write a script to generate first 20 Palindromic Prime Cyclops
    Numbers.


    A cyclops number is a number with an odd number of digits that
    has a zero in the center only.


    Output
    101, 16061, 31013, 35053, 38083, 73037, 74047, 91019, 94049,
    1120211, 1150511, 1160611, 1180811, 1190911, 1250521, 1280821,
    1360631, 1390931, 1490941, 1520251

A solution may be obtained by generating numbers in sequence,
skipping those that contain zeroes, reversing them to
construct a cyclops number and testing it for primality. This
fits into a oneliner.

    perl -MMath::Prime::Util=is_prime -d -E '
    $i=1; while($n<20){$c=$i."0".reverse $i; ++$n, print "$c " if $i!~/0/ && is_prime($c); ++$i}
    '|fmt

Results:

    101 16061 31013 35053 38083 73037 74047 91019 94049 1120211 1150511
    1160611 1180811 1190911 1250521 1280821 1360631 1390931 1490941 1520251

The full code is

    # Perl weekly challenge 177
    # Task 2: Palyndromic prime cyclops
    #
    # See https://wlmb.github.io/2022/08/08/PWC177/#task-2-palindromic-prime-cyclops
    use v5.36;
    use Math::Prime::Util qw(is_prime);
    my $left=1;
    my $count=0;
    my $how_many=shift//20;
    while($count<$how_many){
        my $candidate=$left."0".reverse $left;
        ++$count, print "$candidate " if $left!~/0/ && is_prime($candidate);
        ++$left
    }

    ./ch-2.pl 20|fmt

Results:

    101 16061 31013 35053 38083 73037 74047 91019 94049 1120211 1150511
    1160611 1180811 1190911 1250521 1280821 1360631 1390931 1490941 1520251

Here is a version in Emacs-Lisp.

    (princ (cl-loop for i from 1 upto 200 for s = (number-to-string i)
      for m = (string-match "0" s) for c = (string-to-number(concat s "0" (reverse s)))
      for p = (car (math-prime-test c 1)) for d = (and (not m) p) for n = 0 then (if d (+ n 1) n)
      while (< n 20) if d  collect c )))

Results:

    (101 16061 31013 35053 38083 73037 74047 91019 94049 1120211
    1150511 1160611 1180811 1190911 1250521 1280821 1360631
    1390931 1490941 1520251)
